#BNF+EMV2.1;
!grammar dial_grammar;
!start <Wakeup>;
!start <Dial>;
!start <Dial_Special_Num>;
!start <Contact>;
!start <Dial_Confirm>;
!start <Answer_Confirm>;
!start <Hang_Up>;
!start <Select_Num>;
!start <Open_Google_Now>;
!start <Find_Phone>;
!slot <wakeup_word>;
!slot <contacts>;

<Wakeup>:<wakeup_word>;
<Dial>:[<Wakeup>]<W_Dial>;
<Dial_Special_Num>:[<Wakeup>]<W_Dial_Special_Num>;
<Contact>:[<Wakeup>]<W_Contact>;
<Dial_Confirm>:[<Wakeup>]<W_Dial_Confirm>;
<Hang_Up>:[<Wakeup>]<W_Hang_Up>;
<Answer_Confirm>:[<Wakeup>]<W_Answer_Confirm>;
<Select_Num>:[<Wakeup>]<W_Select_Num>;
<Open_Google_Now>:[<Wakeup>]<W_Open_Google_Now>;
<Find_Phone>:[<Wakeup>]<W_Find_Phone>;

<W_Dial> : <W_Dial_1> | <W_Dial_2>;
<W_Dial_1> : <Dial_Action_1>;
<W_Dial_2> : <Dial_Action_2>;
<W_Dial_Special_Num> : <CallSpecialNumber>;
<W_Hang_Up>: <Reject>;
<W_Contact> : <contacts>;
<W_Dial_Confirm> : <OK> | <Cancel>;
<W_Answer_Confirm> : <Answer> | <Reject>;
<W_Select_Num> : <Select_1> | <Select_2> | <Select_3> | <Select_4> | <Select_5> | <Select_6>;
<W_Open_Google_Now> : открой Google Now;
<W_Find_Phone> : где ты находишься | ты где находишься | [а] ты где | [а] где ты; // don't know which is best
<Dial_Action_1> : <CallName> | <CallNameAtLocation> | <CallCommandStandalone>;
<Dial_Action_2> : <Redial>;

<CallName>:
   <CallCommandSlot_Dative> [абоненту | контакту] <contacts>
 | <CallCommandSlot_Dative> (абоненту | контакту) (по имени | с именем) <contacts>
 | <CallCommandSlot_Accusative> [абонента | контакта] <contacts>
 | <CallCommandSlot_Accusative> (телефон | номер) [абонента | контакта] <contacts>
 | <CallCommandSlot_Accusative> [телефон | номер] контакт <contacts>
 | <CallCommandSlot_Instrumental> с [абонентом | контактом] <contacts>
 | <CallCommandSlot_Instrumental> с (абонентом | контактом) (по имени | с именем) <contacts>
 | <contacts> <CallCommandSlot_Dative>
 | <contacts> <CallCommandSlot_Accusative>
 | <contacts> (телефон | номер) <CallCommandSlot_Accusative>
 | <contacts> <CallCommandSlot_Accusative> (телефон | номер)
 | с <contacts> <CallCommandSlot_Instrumental>
;

<CallNameAtLocation>:
   <CallCommandSlot_Dative> [абоненту | контакту] <contacts> <AtPhoneLocation>
 | <CallCommandSlot_Accusative> [абонента | контакта | контакт] <contacts>
 | <CallCommandSlot_Dative> <AtPhoneLocation> [абоненту | контакту] <contacts>
 | <CallCommandSlot_Dative> <AtPhoneLocation> (абонента | контакта) <contacts>
 | <CallCommandSlot_Accusative> <AtPhoneLocation> [абонента | контакта] <contacts>
 | <CallCommandSlot_Instrumental> с <contacts> <AtPhoneLocation>
;

<CallSpecialNumber>: 
 <CallCommandSlot_Dative> [на] [телефон | номер] <Special_Num>
 | <CallCommandSlot_Dative> по (номеру | телефону) <Special_Num>
 | <CallCommandSlot_Accusative> [телефон | номер] <Special_Num>
 | <CallCommandSlot_Instrumental> с номером <Special_Num>
 | <Special_Num> (вызов | набор | звонок);

<CallCommandSlot_Dative>:
  позвонить 
 | звонить 
 | позвони 
 | звони 
 | [давай] позвоним
 | звоним  
 | сделать звонок  
 | [сделай] звонок
;

 <CallCommandSlot_Accusative>:
  набрать
 | вызвать
 | набери
 | вызови
 | вызов
 | набираем
 | давай наберём
 | вызываем
 | давай вызовем
 | набери мне
; 

<CallCommandSlot_Instrumental>:
  соединить [меня]
 | соедини [меня]
 | соединяемся
; 

<CallCommandStandalone>:
     позвонить
    | звонить 
    | позвони
    | звони
    | звоним
    | [давай] позвоним
    | набрать номер [телефона]
    | набери номер [телефона]
    | набираем номер [телефона]
    | набираем номерок
    | давай наберём номер [телефона]
    | [сделать] звонок
    | телефон
;
 
// ======================================================================
//                            REDIAL COMMANDS
// ======================================================================

<Redial>:
    (позвонить | звонить | позвони | звони) на последний номер [снова | ещё раз]
   | (позвонить | звонить | позвони | звони) (снова | ещё раз) [на последний номер]
   | (перезвонить | перезвони) [снова | ещё раз]
   | (набрать | набери | набери мне) (снова | ещё раз) последний (номер | вызов)
   | (набрать | набери | набери мне) последний (номер | вызов) [снова | ещё раз] 
   | (повторить | повтори) [снова | ещё раз] [последний] (звонок | вызов)
   | повторный (набор | вызов)
;

// ======================================================================
//                       CALL CONFIRMATION COMMANDS
// ======================================================================

<OK>: !tag(Ok, 
   звони | <Yes>
);

<Cancel>: !tag(Cancel, 
	(отменить | отмени) [звонок]
   | (остановить | останови) звонок
   | (прервать | прерви) звонок
   | (отмена | отбой | отставить) 
);
  
<AtPhoneLocation>:
  на [его | её] <PhoneLocationAccusative> [телефон | номер]
  | по [его | её] <PhoneLocationDative> [телефону | номеру]
;

<PhoneLocationAccusative>:
!tag(Number_Type_TAG, 
	домашний !id(1)
  | стационарный !id(1)
  | рабочий !id(2)
  | казённый !id(2)
  | корпоративный !id(2)
  | служебный !id(2)
  | мобильный !id(3)
  | мобильник !id (3)
  | мобилу !id(3)
  | трубу !id (3)
  | трубку !id(3)
  | сотовый !id(3)
);

<PhoneLocationDative>:
!tag(Number_Type_TAG, 
	домашнему !id(1)
  | стационарному !id(1)
  | рабочему !id(2)
  | казённому !id(2)
  | корпоративному !id(2)
  | служебному !id(2)
  | мобильному !id(3)
  | мобильнику !id(3)
  | мобиле !id(3)
  | трубе !id (3)
  | трубке !id(3)
  | сотовому !id(3)
);

<Answer>: !tag(Answer, ответь | ответить );
<Reject>: !tag(Reject, не отвечай | не отвечать );

<Select_1>: !tag(Select_1, первый | первая | первое );
<Select_2>: !tag(Select_2, второй | вторая | второе );
<Select_3>: !tag(Select_3, третий | третья | третье );
<Select_4>: !tag(Select_4, четвёртый | четвёртая | четвёртое );
<Select_5>: !tag(Select_5, пятый | пятая | пятое );
<Select_6>: !tag(Select_6, шестой | шестая | шестое );

<Special_Num>: !tag (Special_Num, 101 | 102 | 103 | 104 | 112 | 1 0 1 | 1 0 2 | 1 0 3 | 1 0 4 | 1 1 2);

<Yes>:
    да
  | ага
  | угу
  | да-да
  ;